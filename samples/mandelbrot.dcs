func print_density(d)
    case
    when d > 8.0
        print(' ')
    when d > 4.0
        print('.')
    when d > 2.0
        print('*')
    else
        print('+')
    end
end

func mandel_converge'(real, imag, iters, creal, cimag)
    return iters if iters > 255.0 || real*real + imag*imag >= 4.0

    return mandel_converge'(
        real*real - imag*imag + creal,
        2.0*real*imag + cimag,
        iters+1.0,
        creal,
        cimag
    )
end

func mandel_converge(real, imag)
    return mandel_converge'(real, imag, 0.0, real, imag)
end

func mandel'(xmin, xmax, xstep, ymin, ymax, ystep)
    var y := ymin
    for y < ymax
        var x := xmin
        for x < xmax
            print_density(mandel_converge(x, y))
            x += xstep
        end
        println("")
        y += ystep
    end
end

func mandel(realstart, imagstart, realmag, imagmag)
    mandel'(realstart, realstart+realmag*78.0, realmag, imagstart, imagstart + imagmag*40.0, imagmag)
end

func main
    mandel(-2.3, -1.3, 0.05, 0.07)
end

